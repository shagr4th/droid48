apply plugin: 'com.android.application'
android {
    namespace 'org.ab.x48'
    signingConfigs {
        release {
            def Properties localProps = new Properties()
            localProps.load(new FileInputStream(file('../local.properties')))
            assert localProps['droid48.store'];
            storeFile file(localProps["droid48.store"])
            keyAlias localProps["droid48.alias"]
            storePassword localProps["droid48.storePass"]
            keyPassword localProps["droid48.pass"]
        }
    }
    compileSdkVersion 33
    ndkVersion "25.2.9519653"

    defaultConfig {
        applicationId "org.ab.x48"
        minSdkVersion 16
        targetSdkVersion 33

        externalNativeBuild {
            cmake {
                arguments "-DANDROID_PLATFORM=android-16"
            }
        }
    }

    externalNativeBuild {
        cmake {
            path "src/main/jni/CMakeLists.txt"
        }
    }

    buildTypes {
        release {
            //can't minify cause I use Java names on JNI side
            //minifyEnabled true
            //proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }

        debug {
            debuggable true
            jniDebuggable true
            minifyEnabled false
            shrinkResources false
        }
    }

}
dependencies {
    implementation 'com.getkeepsafe.relinker:relinker:1.4.5'
}